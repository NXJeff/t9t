 /*
  * Copyright 2016 arvato Systems GmbH
  *
  */
import api.*
import meta.*
import t9t.base.api.*
import t9t.base.types.*

package (com.arvatosystems) t9t.translation.request {
    default private unsigned trim noControlChars;
    properties noJava, simpleref,
        tenantCategory is com.arvatosystems.t9t.annotations.TenantCategory(),
        searchprefix is com.arvatosystems.t9t.annotations.SearchPrefix(),
        serialized is com.arvatosystems.t9t.annotations.StoreSerialized,
        noAutoMap is com.arvatosystems.t9t.annotations.NoAutoMap,
        notupdatable is com.arvatosystems.t9t.annotations.NotUpdatable,
        ref is com.arvatosystems.t9t.annotations.IsObjectRef;

    class UILanguageDTO {
        required languageTag                                languageCode;
        required shortText                                  languageNameEnglish;
        required shortText                                  languageNameNative;
    }

    class GetLanguagesRequest extends ContextlessRequestParameters return GetLanguagesResponse {
    }

    class GetLanguagesResponse extends ServiceResponse {
        required (UILanguageDTO...) required List<>         languages;
    }


    /** Retrieves the translations for all enums in the current language, or an explicitly requested one. */
    class GetEnumsTranslationRequest extends RequestParameters return GetEnumsTranslationResponse {
        required identifier required List<>                 enumPQONs;
        optional languageTag                                overrideLanguage; // fall back from de_AT to de to en in case no other translation exists, defaults to true
        optional Boolean                                    useFallback;      // request translations for this specific language, not the current one as set in the JWT
    }

    class EnumInstanceDTO {
        required identifier                                 enumInstanceName;
        required shortText                                  enumInstanceTranslation;
    }

    class EnumTranslationDTO {
        required identifier                                 enumPQON;
        required (EnumInstanceDTO...) required List<>       instances;
    }

    class GetEnumsTranslationResponse extends ServiceResponse {
        required (EnumTranslationDTO...) required List<>    translations;
    }
}
